ext {
    libraries = [
            slf4j        : '1.7.25',
            lombok       : '1.18.10',
            jsr330       : '1',
            jsr250       : '1.3.2',
            javassist    : '3.26.0-GA',
            cglib        : '3.3.0',
            objenesis    : '3.1',
            aspectjweaver: '1.9.4',
            snakeyaml    : '1.25',
            vertx        : '3.8.4',
            hessian      : '4.0.63'
    ]
}

allprojects {
    group = 'com.github.bdqfork'
    version = '0.4'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    repositories {
        mavenLocal()
        maven {
            url = 'http://repo.maven.apache.org/maven2'
        }
    }

    dependencies {
        implementation 'org.slf4j:slf4j-api:1.7.25'
        implementation 'ch.qos.logback:logback-classic:1.2.3'
        implementation 'ch.qos.logback:logback-core:1.2.3'
        testImplementation 'junit:junit:4.12'

        annotationProcessor 'org.projectlombok:lombok:1.18.10'
        compileOnly 'org.projectlombok:lombok:1.18.10'
    }

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
        classifier = 'sources'
    }

    task javadocJar(type: Jar) {
        from javadoc
        classifier = 'javadoc'
    }

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none','-quiet')
        options.addStringOption('encoding', 'UTF-8')
        options.addStringOption('charSet', 'UTF-8')
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = 'festival-parent'
                    description = 'A responsive web framework with ioc function based on vertx.'
                    url = 'https://github.com/bdqfork/festival'
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            name = 'bdqfork'
                            email = 'bdqsky@163.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git@github.com:bdqfork/festival.git'
                        developerConnection = 'scm:git:git@github.com:bdqfork/festival.git'
                        url = 'https://github.com/bdqfork/festival'
                    }
                }

            }
            maven(MavenPublication) {
                from(components.java)
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                credentials {
                    username ossrh_key
                    password ossrh_pass
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}
